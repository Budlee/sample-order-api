buildscript {
    ext {
        springBootVersion = '2.1.5.RELEASE'
        verifier_version = '2.1.1.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:${verifier_version}"
    }
}


apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'groovy'
apply plugin: 'spring-cloud-contract'


group = 'com.budlee'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 12

repositories {
    mavenCentral()
}
dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:${verifier_version}"
    }
}

test {
    useJUnitPlatform()
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
}

check.dependsOn integrationTest

contracts {
    testFramework = 'JUNIT5'
    packageWithBaseClasses = 'com.budlee.orderapi.contracts'
    contractsDslDir = new File("${project.rootDir}/src/test/resources/contracts/")
}

dependencies {
    implementation "io.springfox:springfox-swagger2:2.9.2"
    implementation "io.springfox:springfox-swagger-ui:2.9.2"
    implementation("org.springframework.boot:spring-boot-starter-web:2.1.1.RELEASE") {
        exclude module: "spring-boot-starter-tomcat"
    }
    implementation("org.springframework.boot:spring-boot-starter-data-mongodb:2.1.1.RELEASE")
    implementation("org.springframework.boot:spring-boot-starter-jetty:2.1.1.RELEASE")

    implementation group: 'com.googlecode.libphonenumber', name: 'libphonenumber', version: '8.10.2'

    implementation('org.springframework.boot:spring-boot-starter')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: '1.11.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
    integrationTestImplementation('org.springframework.boot:spring-boot-starter-test')
    integrationTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
    integrationTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine'

//    Spring Cloud Contracts
    testImplementation 'org.codehaus.groovy:groovy-all:2.4.6'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'

//    Lombok
    implementation "org.projectlombok:lombok"
    annotationProcessor 'org.projectlombok:lombok'

}
